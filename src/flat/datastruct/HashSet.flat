package flat/datastruct

import flat/datastruct/list/List
import flat/datastruct/list/Iterator
import flat/datastruct/list/NoSuchElementException

class HashSet<T> implements List<T> {
  visible var Int count <=> count

  T[][] buckets

  visible HashSetIterator<T> iterator => HashSetIterator(this)

  public T first => null
  public T last => null

  public construct(Int bucketCount = 5, private Int bucketSize = 5) {
    buckets = Array(bucketCount).map({ Array<T>(bucketSize) })
  }

  public construct(T[] values, Int bucketCount = 5, private Int bucketSize = 5) {
    this(bucketCount, bucketSize)
    values.forEach({ add(_) })
  }

  override public toArray() => map({ _ })

  override public any(anyFunc(T x, Int i, HashSet<T> set) -> Bool) => false {
    var Int i = 0

    for (bucket in buckets) {
      for (v in bucket) {
        if (v && anyFunc(v, i++, this)) {
          return true
        }
      }
    }
  }

  override public all(allFunc(T x, Int i, HashSet<T> set) -> Bool, Bool stopOnContradiction = true) -> Bool {
    var contradiction = false
    var Int i = 0

    for (bucket in buckets) {
      for (v in bucket) {
        if (v && !allFunc(v, i++, this)) {
          if (stopOnContradiction) {
            return false
          }

          contradiction = true
        }
      }
    }

    return !contradiction
  }

  override public map<Out>(func(T, Int, HashSet<T>) -> Out) -> Out[] {
    let array = Out[]

    var Int i = 0

    for (bucket in buckets) {
      for (v in bucket) {
        if (v) {
          array.add(func(v, i++, this))
        }
      }
    }

    return array
  }

  override public filter(func(T x, Int i, HashSet<T> set) -> Bool) -> T[] {
    let array = T[]

    var Int i = 0

    for (bucket in buckets) {
      for (v in bucket) {
        if (v && func(v, i++, this)) {
          array.add(v)
        }
      }
    }

    return array
  }

  override public join(
    String delimiter = "",
    var Int maxOutputCount = count,
    Int ellipsePosition = maxOutputCount / 2 + 1,
    String ellipse = "..."
  ) -> String {
    var Int i = 0

    var output = ""

    for (bucket in buckets) {
      for (v in bucket) {
        if (v) {
          if (i > 0) {
            output = output + delimiter
          }

          output = output + v
        }
      }
    }

    return output
  }

  override public skip(Int num) => map({ _ }).skip(num)
  override public take(Int num) => map({ _ }).take(num)
  override public reverse() => map({ _ }).reverse()

  override public firstWhere(func(T x, Int i, HashSet<T> set) -> Bool) -> T => null {
    var Int i = 0

    for (bucket in buckets) {
      for (v in bucket) {
        if (v && func(v, i++, this)) {
          return v
        }
      }
    }
  }

  override public forEach(func(T x, Int i, HashSet<T> set)) => this {
    var Int i = 0

    for (bucket in buckets) {
      for (v in bucket) {
        if (v) {
          func(v, i++, this)
        }
      }
    }
  }

  getBucket(T value) => buckets[(Int)(value.hashCodeLong & (buckets.count - 1))]

  public add(T value) => value {
    addIfNotExists(value)
  }

  public addIfNotExists(T value) => false {
    let bucket = getBucket(value)

    if (!bucket.contains(value)) {
      bucket.add(value)
      count = count + 1

      return true
    }
  }

  public get(T value) => getBucket(value).firstWhere({ _x && _x.equals(value) })

  public remove(T value) -> T => null {
    T[] bucket = getBucket(value)

    var Int i = 0

    for (v in bucket) {
      if (v && v.equals(value)) {
        bucket.remove(i)
        count = count - 1

        return v
      }

      i++
    }
  }

  override public contains(T value) => value && get(value)

  public toString() => "HashSet [#{join(", ")}]"

  override public equals(Object other) => (other || !this) && other.class.isOfType(HashSet.class) && equals((HashSet)other)
  public equals(HashSet<T> other) => true {
    if (count != other.count) return false

    for (element in this) {
      if (!other.contains(element)) return false
    }
  }

  class HashSetIterator<K> implements Iterator<K> {
    visible var Int position

    var Iterator<Array<K>> bucketIterator
    var Iterator<K> valueIterator

    visible Bool hasNext => position < set.count && (bucketIterator.hasNext || valueIterator?.hasNext)
    visible Bool hasPrevious => bucketIterator.hasPrevious || valueIterator?.hasPrevious

    visible K stepNext {
      get {
        if (valueIterator?.hasNext) {
          position++
          return valueIterator.stepNext
        } else if (bucketIterator.hasNext) {
          position++
          valueIterator = bucketIterator.stepNext.iterator
          return valueIterator.stepNext
        }

        throw NoSuchElementException()
      }
    }

    visible K current => valueIterator?.current
    visible K previous {
      get {
        if (valueIterator?.hasPrevious) {
          return valueIterator.previous
        } else if (bucketIterator.hasPrevious) {
          return bucketIterator.previous.last
        } else {
          return null
        }
      }
    }
    visible K next {
      get {
        if (valueIterator?.hasNext) {
          return valueIterator.next
        } else if (bucketIterator.hasNext) {
          return bucketIterator.next.first
        } else {
          return null
        }
      }
    }

    public construct(visible HashSet<K> set) {
      reset()
    }

    public reset() => this {
      position = 0
      bucketIterator = set.buckets.iterator
      valueIterator = bucketIterator.hasNext ? bucketIterator.stepNext.iterator : null
    }
  }
}